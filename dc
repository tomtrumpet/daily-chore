// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© austint01

//@version=5
strategy("Triple EMA Trend Continuation", overlay = true, initial_capital = 100000, pyramiding = 5, max_bars_back = 5000)
import PineCoders/Time/3

input_trade_limited_range = input.bool(true, title = "Trade limited date range")
input_trade_start_time = input.time(timestamp("1 Apr 2004"), title='Start Time')
input_no_trade_friday = input(true, title = "Ignore Friday trades?")
input_trending_candles = input(false, "Trending Candle (close towards trend)")
input_candle_fraction = input(0.5, "Open and close within fraction (as a decimal between 0 and 1) of candle high/low. e.g. 0.333 = 1/3")
input_ema_fanning_out = input(true, "EMA fanning out (trend becoming established)")
input_ema_fast = input(8, "EMA Fast")
input_ema_mid = input(20, "EMA Mid")
input_ema_slow = input(50, "EMA Slow")
input_risk_to_reward = input(1.0, "Risk to Reward as a ratio of X:1")
input_risk_account_percentage = input(1.0, "Risk percentage of account per trade")
input_ema_fanning_threshold_days = 1
input_pips_beyond_candle = input(1, "Pips beyond candle for stops")
input_signal_is_strong = input(true, "Signal candle is strong")

GetPipSize() => syminfo.mintick * (syminfo.type == "forex" ? 10 : 1)
GetTicketFactor() => 1 / syminfo.mintick

ema_fast = ta.ema(close, input_ema_fast)
ema_mid = ta.ema(close, input_ema_mid)
ema_slow = ta.ema(close, input_ema_slow)

can_trade = false // assume we can't trade next bar market

// general calculations used for decision making

bullish = ema_fast > ema_mid and ema_mid > ema_slow
bearish = ema_fast < ema_mid and ema_mid < ema_slow

candle_length = high - low
candle_length_fraction = candle_length * input_candle_fraction

candle_threshold = high - candle_length_fraction //bullish
if bearish
    candle_threshold := low + candle_length_fraction

// Trending Candle

trade_trending_candles = true

if input_trending_candles
    if bullish and close < open
        trade_trending_candles := false
    else if bearish and close > open
        trade_trending_candles := false

// Trade limited date range

trade_full_date_range = true

if input_trade_limited_range and time < input_trade_start_time
    trade_full_date_range := false

// EMA fanning

ema_fast_past = ta.ema(close[input_ema_fanning_threshold_days], input_ema_fast)
ema_mid_past = ta.ema(close[input_ema_fanning_threshold_days], input_ema_mid)
ema_slow_past = ta.ema(close[input_ema_fanning_threshold_days], input_ema_slow)

ema_fast_paster = ta.ema(close[input_ema_fanning_threshold_days * 2], input_ema_fast)
ema_mid_paster = ta.ema(close[input_ema_fanning_threshold_days * 2], input_ema_mid)
ema_slow_paster = ta.ema(close[input_ema_fanning_threshold_days * 2], input_ema_slow)


trade_ema_fanning_out = true

if input_ema_fanning_out and input_ema_fanning_threshold_days > 0
    if bullish and not (ema_fast > ema_fast_past and ema_fast_past > ema_fast_paster and ema_mid > ema_mid_past and ema_mid_past > ema_mid_paster and ema_slow > ema_slow_past and ema_slow_past > ema_slow_paster)
        trade_ema_fanning_out := false
    else if bearish and not (ema_fast < ema_fast_past and ema_fast_past < ema_fast_paster and ema_mid < ema_mid_past and ema_mid_past < ema_mid_paster and ema_slow < ema_slow_past and ema_slow_past < ema_slow_paster)
        trade_ema_fanning_out := false

// Touching Fast EMA

can_trade := (bullish and (low <= ema_fast)) or (bearish and (high >= ema_fast))

// Open and Close within threshold

// plot(candle_threshold, color = color.white, title = "candle threshold")
can_trade := can_trade and ((bullish and open > candle_threshold and close > candle_threshold) or (bearish and open < candle_threshold and close < candle_threshold)) // work out if open and close in trading threshold

// Trade on Friday?

trade_on_friday = true

if input_no_trade_friday and dayofweek(time) == 4 // No Friday (by excluding Thursday (4) setups)
    trade_on_friday := false

// Input signal is after a retracement?
// input_signal_after_retracement
// input_retracement_candles_count

trade_without_strong_signal = true

//and ((bullish and low > low[1]) or (bearish and high < high[1])) - wick is a deeper retracement than last candle
//and high-low < high[1]-low[1] - wick is bigger than last candle

// wick is a deeper retracement than last 3 (inclusive) candles
if input_signal_is_strong and (bullish and low > ta.lowest(3) or bearish and high < ta.highest(3)) 
    trade_without_strong_signal := false

// Can we trade or not?

can_trade := can_trade and trade_trending_candles and trade_ema_fanning_out and trade_full_date_range and trade_on_friday and trade_without_strong_signal

// Cancel trade if not entered after signal on next bar market

// if ta.barssince(can_trade) == 1 and strategy.opentrades == 0
//     strategy.cancel_all()

plot(ema_fast, color = color.green)
plot(ema_mid, color = color.orange)
plot(ema_slow, color = color.red)

bgcolor(can_trade ? color.white : na)

var float entry_price = na
var float stop_loss_price = na
var float target_price = na

float open_trade_stop_loss_price = na
float open_trade_entry_price =  na
float open_trade_entry_stop_diff = na
float open_trade_target_price = na

// float trailing_ema = ta.sar(0.02, 0.02, 0.2)
float trailing_ema = ta.ema(close, 13)

float equity_risked = (input_risk_account_percentage / 100) * strategy.equity
int open_trade_bar_index = ta.barssince(can_trade)

// trade entries

if can_trade
    if bullish
        stop_loss_price := low  -  (input_pips_beyond_candle * GetPipSize()) // input_pips_beyond_candle pips below low
        entry_price := high + (input_pips_beyond_candle * GetPipSize()) // input_pips_beyond_candle pips ahove high
        entry_stop_diff = entry_price - stop_loss_price
        target_price := entry_price + (entry_stop_diff * input_risk_to_reward)
        position_size = equity_risked / entry_stop_diff
        
        strategy.entry("long", strategy.long, qty = position_size, stop = entry_price)
        strategy.exit("long_tp1", from_entry = "long", qty_percent = 75, limit = target_price, stop = stop_loss_price)
        strategy.exit("long_stop", from_entry = "long", stop = stop_loss_price)
    else if bearish
        stop_loss_price := high  +  (input_pips_beyond_candle * GetPipSize()) // input_pips_beyond_candle pips above high
        entry_price := low - (input_pips_beyond_candle * GetPipSize()) // input_pips_beyond_candle pips below low
        entry_stop_diff = stop_loss_price - entry_price
        target_price := entry_price - (entry_stop_diff * input_risk_to_reward)
        position_size = equity_risked / entry_stop_diff

        strategy.entry("short", strategy.short, qty = position_size, stop = entry_price)
        strategy.exit("short_tp1", from_entry = "short", qty_percent = 75, limit = target_price, stop = stop_loss_price)
        strategy.exit("short_stop", from_entry = "short", stop = stop_loss_price)

//long

long_stop_price = 0.0
long_stop_price := if (strategy.position_size > 0)
    math.max(trailing_ema, long_stop_price[1], target_price)
else
    0

if strategy.position_size > 0
    if close[0] > target_price
        strategy.cancel("long_stop")
        strategy.exit("long_trailing_sl", from_entry = "long", qty_percent = 100, stop = long_stop_price)
    else if close[0] > entry_price
        strategy.cancel("long_stop")
        strategy.exit("long_stop", from_entry = "long", stop = entry_price)

plot(series=strategy.position_size > 0 and close[0] > target_price ? long_stop_price : na, color=color.new(#40ccfb, 0), style=plot.style_cross, linewidth=3)

//short

short_stop_price = 0.0
short_stop_price := if (strategy.position_size < 0)
    math.min(trailing_ema, short_stop_price[1], target_price)
else
    999999

if strategy.position_size < 0
    if close[0] < target_price
        strategy.cancel("short_stop")
        strategy.exit("short_trailing_sl", from_entry = "short", qty_percent = 100, stop = short_stop_price)
    else if close[0] < entry_price
        strategy.cancel("long_stop")
        strategy.exit("short_stop", from_entry = "short", stop = entry_price)

plot(series=strategy.position_size < 0 and close[0] < target_price ? short_stop_price : na, color=color.new(#40ccfb, 0), style=plot.style_cross, linewidth=3)
